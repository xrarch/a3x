#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

externconst Font

(* ebus dma controller driver *)

procedure BuildDMA (* -- *)
	DeviceNew
		"dma" DSetName

		"dma-controller" "deviceType" DAddProperty

		"AISA,ebus-dma" "model" DAddProperty

		pointerof DMATransfer "transfer" DAddMethod
		pointerof DMABitTransfer "bitTransfer" DAddMethod
	DeviceExit

	DMAWaitUnbusy
	0 DMARegisterStatus!
end

procedure DMAWaitUnbusy (* -- *)
	while (DMARegisterStatus@ 1 &) end
end

procedure DMADoOperation (* -- *)
	DMARegisterStatus@ 1 | DMARegisterStatus!
end

procedure DMAFullTransfer { src dest srcinc destinc count mode lines destmod srcmod -- }
	src@ DMARegisterSource!
	dest@ DMARegisterDest!
	srcinc@ DMARegisterSInc!
	destinc@ DMARegisterDInc!
	count@ DMARegisterCount!
	mode@ DMARegisterMode!
	lines@ DMARegisterLines!
	destmod@ DMARegisterDestMod!
	srcmod@ DMARegisterSrcMod!

	auto rs
	InterruptDisable rs!

	DMADoOperation
	DMAWaitUnbusy

	rs@ InterruptRestore
end

procedure DMABitTransfer (* src dest srcinc destinc count mode lines destmod srcmod *) { direction clearbyte setbyte -- }
	1 DMARegisterBitMode!
	clearbyte@ DMARegisterClearByte!
	setbyte@ DMARegisterSetByte!
	direction@ DMARegisterDirection!

	DMAFullTransfer
end

procedure DMATransfer (* src dest srcinc destinc count mode lines destmod srcmod -- *)
	DMAFullTransfer
end