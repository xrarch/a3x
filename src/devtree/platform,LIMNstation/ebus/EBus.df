#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

extern BuildDMA { -- }
extern BuildPBoard { -- }
extern BuildLIMNGFX { -- }
extern BuildSNVRAM { -- }
extern BuildMemory { -- }

fn BuildEBus { -- }
	DeviceNew
		"ebus" DSetName

		"bus" "deviceType" DAddProperty

		"AISA,ebus" "model" DAddProperty

		1 "revision" DAddProperty

		pointerof EBusFindFirstBoard "ebus,findFirstBoard" DAddMethod
		pointerof EBusSlotInterruptRegister "ebus,slotInterruptRegister" DAddMethod
		pointerof EBusReset "reset" DAddMethod

		EBusSlotsStart "ebus,slotsBase" DAddProperty

		EBusSlotSpace "ebus,slotSize" DAddProperty

		EBusSlots "ebus,slotCount" DAddProperty

		BuildSNVRAM

		BuildMemory

		BuildDMA

		BuildPBoard

		BuildLIMNGFX
	DeviceExit
end

(* find first board of id *)
fn EBusFindFirstBoard { id -- slot }
	auto i
	0 i!

	while (i@ EBusSlots <)
		auto bp
		i@ EBusSlotSpace * EBusSlotsStart + bp!

		if (bp@@ EBusBoardMagic ==)
			if (bp@ 4 + @ id@ ==)
				i@ slot!
				return
			end
		end

		1 i +=
	end

	ERR slot!
	return
end

fn EBusSlotInterruptRegister { handler slot -- }
	handler@ slot@ 0x98 + InterruptRegister
end

asm "

EBusReset:
	lui rs, 0x80000000
	ret

"