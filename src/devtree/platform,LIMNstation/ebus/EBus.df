#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

extern BuildPBoard { -- }
extern BuildKinnowFB { -- }
extern BuildSNVRAM { -- }
extern BuildMemory { -- }

const PBoardReset 0xF8800000
const PBoardResetMagic 0xAABBCCDD

fn BuildEBus { -- }
	DeviceNew
		"ebus" DSetName

		"bus" "deviceType" DAddProperty

		"AISA,ebus" "model" DAddProperty

		1 "revision" DAddProperty

		pointerof EBusFindFirstBoard "ebus,findFirstBoard" DAddMethod
		pointerof EBusSlotInterruptRegister "ebus,slotInterruptRegister" DAddMethod
		pointerof EBusReset "reset" DAddMethod

		EBusSlotsStart "ebus,slotsBase" DAddProperty

		EBusSlotSpace "ebus,slotSize" DAddProperty

		EBusSlots "ebus,slotCount" DAddProperty

		BuildSNVRAM

		BuildMemory

		BuildPBoard

		BuildKinnowFB
	DeviceExit
end

(* find first board of id *)
fn EBusFindFirstBoard { id -- slot }
	auto i
	0 i!

	while (i@ EBusSlots <)
		auto bp
		i@ EBusSlotSpace * EBusSlotsStart + bp!

		if (bp@@ EBusBoardMagic ==)
			if (bp@ 4 + @ id@ ==)
				i@ slot!
				return
			end
		end

		1 i +=
	end

	ERR slot!
	return
end

fn EBusSlotInterruptRegister { handler slot -- }
	handler@ slot@ 0x98 + InterruptRegister
end

fn EBusReset { -- }
	PBoardResetMagic PBoardReset!
end