#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

const AHDBCmdPort 0x19
const AHDBPortA 0x1A
const AHDBPortB 0x1B

const AHDBCmdSelect 0x1
const AHDBCmdRead 0x2
const AHDBCmdWrite 0x3
const AHDBCmdInfo 0x4
const AHDBCmdPoll 0x5

const AHDBBuffer 0xF8020000

var AHDBDMANode 0
var AHDBDMATransfer 0

var AHDBDMAEmulation 0

struct AHDB_VDB
	16 Label
	128 PartitionTable
	4 Magic
endstruct

struct AHDB_PTE
	8 Label
	4 Blocks
	1 Status
	3 Unused
endstruct

procedure private AHDBDMATransferBlock { src dest -- }
	if (AHDBDMANode@ 0 ==)
		auto ndma
		"/dma" DevTreeWalk ndma!

		if (ndma@ 0 ~=)
			ndma@ AHDBDMANode!

			ndma@ DeviceSelectNode
				"transfer" DGetMethod AHDBDMATransfer!
			DeviceExit
		end
	end

	if (AHDBDMANode@ 0 ~=)
		AHDBDMANode@ DeviceSelectNode
			src@ dest@
			4 4
			1024
			2
			AHDBDMATransfer@ DCallMethodPtr
		DeviceExit
	end else
		if (AHDBDMAEmulation@ PlatformInited@ ~~ ||)
			(* just copy in software if early *)
			dest@ src@ 4096 memcpy
		end else
			1 AHDBDMAEmulation!

			"dks: where is /dma? using emulation (SLOW)\n" Problem
		end
	end
end

procedure AHDBBootable { part id -- }
	auto brecord
	4096 Malloc brecord!

	if (brecord@ 1 AHDBRead 1 ==)
		(* is it a valid boot record? *)
		if (brecord@ BootRecord_Magic + @ BootMagic == brecord@ BootRecord_ret + gb BootRM == &&)
			(* yes, it's bootable *)

			brecord@ BootRecord_OSLabel + strdup "osLabel" DAddProperty
		end
	end else
		auto pc
		if (part@ -1 ==)
			'-' pc!
		end else
			part@ 'a' + pc!
		end
		pc@ id@ "dks%d/%l: couldn't read boot record\n" Printf
	end

	brecord@ Free
end

procedure AHDBPartitions { id -- pcount }
	auto vdbuf
	4096 Malloc vdbuf!

	0 pcount!

	if (vdbuf@ 0 AHDBRead 1 ==)
		if (vdbuf@ AHDB_VDB_Magic + @ 0x4E4D494C ==)
			vdbuf@ AHDB_VDB_Label + strdup "diskLabel" DAddProperty

			auto i
			0 i!

			auto pc
			0 pc!

			auto offset
			2 offset!

			while (i@ 8 <)
				auto ptr
				AHDB_PTE_SIZEOF i@ * vdbuf@ + ptr!

				if (ptr@ AHDB_PTE_Status + gb 0 ~=)
					auto char
					2 Calloc char!
					'a' pc@ + char@ sb

					1 pcount +=

					DeviceNew
						char@ DSetName

						id@ "satsuma,id" DAddProperty
						ptr@ AHDB_PTE_Blocks + @ "blocks" DAddProperty
						offset@ "offset" DAddProperty

						"disk-logical" "deviceType" DAddProperty

						ptr@ AHDB_PTE_Label + strdup "diskLabel" DAddProperty

						pointerof AHDBRead "readBlock" DAddMethod
						pointerof AHDBWrite "writeBlock" DAddMethod

						i@ id@ AHDBBootable
					DeviceExit

					ptr@ AHDB_PTE_Blocks + @ offset@ + offset!

					1 pc +=
				end

				1 i +=
			end
		end else
			id@ "dks%d: bad volume descriptor\n" Printf
		end
	end else
		id@ "dks%d: couldn't read blk0\n" Printf
	end

	vdbuf@ Free
end

procedure BuildSatsuma (* -- *)
	DeviceNew
		"dks" DSetName

		"disk-bus" "deviceType" DAddProperty

		"AISA,satsuma" "model" DAddProperty

		0x20 "interrupt#" DAddProperty

		auto i
		0 i!
		while (i@ 8 <)
			auto present
			auto blocks

			i@ AHDBPoll present! blocks!

			if (present@ 1 ==)
				DeviceNew
					auto lilbuf
					5 Calloc lilbuf!
					i@ lilbuf@ itoa
					lilbuf@ DSetName

					i@ "satsuma,id" DAddProperty
					blocks@ "blocks" DAddProperty
					0 "offset" DAddProperty

					"disk" "deviceType" DAddProperty

					"AISA,satsuma-disk" "model" DAddProperty

					pointerof AHDBRead "readBlock" DAddMethod
					pointerof AHDBWrite "writeBlock" DAddMethod

					auto pcount

					i@ AHDBPartitions pcount!

					if (pcount@ 0 ==)
						-1 i@ AHDBBootable
					end

					pcount@ "partitions" DAddProperty
				DeviceExit
			end

			1 i +=
		end
	DeviceExit
end

procedure AHDBRead { ptr block -- ok }
	auto rs
	InterruptDisable rs!

	auto id
	"satsuma,id" DGetProperty id!

	if (block@ "blocks" DGetProperty >=) rs@ InterruptRestore ERR ok! return end

	"offset" DGetProperty block@ + block!

	id@ AHDBSelect

	block@ AHDBPortA DCitronOutl
	AHDBCmdRead AHDBCmdPort DCitronCommand

	AHDBBuffer ptr@ AHDBDMATransferBlock

	rs@ InterruptRestore

	1 ok!
end

procedure AHDBWrite { ptr block -- ok }
	auto rs
	InterruptDisable rs!

	auto id
	"satsuma,id" DGetProperty id!

	if (block@ "blocks" DGetProperty >=) rs@ InterruptRestore ERR ok! return end

	"offset" DGetProperty block@ + block!

	id@ AHDBSelect

	ptr@ AHDBBuffer AHDBDMATransferBlock

	block@ AHDBPortA DCitronOutl
	AHDBCmdWrite AHDBCmdPort DCitronCommand

	rs@ InterruptRestore

	1 ok!
end

procedure AHDBPoll { id -- blocks present }
	auto rs
	InterruptDisable rs!

	id@ AHDBPortA DCitronOutl

	AHDBCmdPoll AHDBCmdPort DCitronCommand

	AHDBPortA DCitronInl present!
	AHDBPortB DCitronInl blocks!

	rs@ InterruptRestore
end

procedure AHDBInfo { -- event details }
	auto rs
	InterruptDisable rs!

	AHDBCmdInfo AHDBCmdPort DCitronCommand
	AHDBPortA DCitronInb event!
	AHDBPortB DCitronInb details!

	rs@ InterruptRestore
end

procedure AHDBSelect { drive -- }
	auto rs
	InterruptDisable rs!

	drive@ AHDBPortA DCitronOutl
	AHDBCmdSelect AHDBCmdPort DCitronCommand

	rs@ InterruptRestore
end