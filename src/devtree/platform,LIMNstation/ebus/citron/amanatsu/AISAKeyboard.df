#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

var AKeyboardDev 0
var AKeyboardCount 0

var AKeyboardID 0

const AKeyboardMID 0x8FC48FC4

fn AKeyboardPoll { -- }
	auto i
	1 i!

	while (i@ 16 <)
		auto rs
		InterruptDisable rs!

		if (i@ AmanatsuPoll AKeyboardMID ==)
			auto hmm
			5 Calloc hmm!

			AKeyboardDev@ DeviceSelectNode
				DeviceNew
					AKeyboardCount@ hmm@ itoa

					hmm@ DSetName

					i@ "amanatsu,unit" DAddProperty

					pointerof AKeyboardReadM "read" DAddMethod

					pointerof AKeyboardIsDownM "isDown" DAddMethod

					pointerof AKeyboardWaitKeyM "waitKey" DAddMethod

					"keyboard" "deviceType" DAddProperty

					"AISA,keyboard" "model" DAddProperty

					2 "revision" DAddProperty

					i@ 4 + "interrupt#" DAddProperty

					1 AKeyboardCount +=
				DeviceExit
			DeviceExit
		end

		rs@ InterruptRestore

		1 i +=
	end
end

fn private AKeyboardWaitKeyM { argvt argcn -- out1 out2 out3 }
	argvt@ 8 + @
	argvt@ 4 + @
	argvt@ @
	AKeyboardWaitKey out1!
end

fn AKeyboardWaitKey { cancelkey waitkey timeout -- pressed }
	0 pressed!

	auto cn
	"/clock" DevTreeWalk cn!

	auto ticks
	timeout@ 100 / 1 max ticks!

	if (cn@ 0 ~=)
		auto i
		0 i!

		while (i@ ticks@ <)
			if (cancelkey@ AKeyboardIsDown)
				0 pressed!
				return
			end

			if (waitkey@ AKeyboardIsDown)
				1 pressed!
				return
			end

			cn@ DeviceSelectNode
				100 "wait" DCallMethod drop drop drop drop
			DeviceExit

			1 i +=
		end
	end
end

fn AKeyboardPopCode { id -- code }
	auto rs
	InterruptDisable rs!

	id@ AmanatsuSelectDev
	1 AmanatsuCommand
	AmanatsuReadA code!

	rs@ InterruptRestore
end

fn AKeyboardSpecial { code -- char }
	if (code@ 50 ==)
		'\n' char!
	end elseif (code@ 51 ==)
		'\b' char!
	end else
		ERR char!
	end
end

fn private AKeyboardIsDownM { argvt argcn -- out1 out2 out3 }
	argvt@ @
	AKeyboardIsDown out1!
end

fn AKeyboardIsDown { scancode -- down }
	auto id
	"amanatsu,unit" DGetProperty id!

	auto rs
	InterruptDisable rs!

	id@ AmanatsuSelectDev
	scancode@ AmanatsuWriteA
	3 AmanatsuCommand
	AmanatsuReadA down!

	if (down@)
		while (id@ AKeyboardPopCode 0xFFFF ~=) end
	end

	rs@ InterruptRestore
end

fn private AKeyboardReadM { argvt argcn -- out1 out2 out3 }
	AKeyboardRead out1!
end

var Shift 0
var Ctrl 0

fn AKeyboardRead { -- c }
	auto id
	"amanatsu,unit" DGetProperty id!

	auto code
	id@ AKeyboardPopCode code!

	ERR c!

	if (code@ 0xFFFF ==)
		return
	end

	if (code@ 80 ==)
		1 Ctrl!
		return
	end elseif (code@ 82 ==)
		1 Shift!
		return
	end elseif (code@ 32848 ==)
		0 Ctrl!
		return
	end elseif (code@ 32850 ==)
		0 Shift!
		return
	end

	if (code@ 80 >=)
		return
	end

	if (code@ 50 >=) code@ AKeyboardSpecial c! return end

	if (Shift@)
		[code@]AKeyboardLayoutShift@ c!
	end elseif (Ctrl@)
		[code@]AKeyboardLayoutCtrl@ c!
	end else
		[code@]AKeyboardLayout@ c!
	end
end

fn AKeyboardInit { -- }
	DeviceNew
		"kbd" DSetName
		DevCurrent@ AKeyboardDev!
	DeviceExit
end

table AKeyboardLayout
	'a'
	'b' 'c' 'd'
	'e' 'f' 'g'
	'h' 'i' 'j'
	'k' 'l' 'm'
	'n' 'o' 'p'
	'q' 'r' 's'
	't' 'u' 'v'
	'w' 'x' 'y'
	'z'
	'0' '1' '2'
	'3' '4' '5'
	'6' '7' '8'
	'9'
	';'
	' '
	' '
	'-'
	'='
	'['
	']'
	'\\'
	0
	'/'
	'.'
	'\''
	','
	'`'
endtable

table AKeyboardLayoutCtrl
	'a'
	'b' 'c' 'd'
	'e' 'f' 'g'
	'h' 'i' 'j'
	'k' 'l' 'm'
	'n' 'o' 'p'
	'q' 'r' 's'
	't' 'u' 'v'
	'w' 'x' 'y'
	'z'
	'0' '1' '2'
	'3' '4' '5'
	'6' '7' '8'
	'9'
	';'
	' '
	' '
	'-'
	'='
	'['
	']'
	'\\'
	0
	'/'
	'.'
	'\''
	','
	'`'
endtable

table AKeyboardLayoutShift
	'A'
	'B' 'C' 'D'
	'E' 'F' 'G'
	'H' 'I' 'J'
	'K' 'L' 'M'
	'N' 'O' 'P'
	'Q' 'R' 'S'
	'T' 'U' 'V'
	'W' 'X' 'Y'
	'Z'
	')' '!' '@'
	'#' '$' '%'
	'^' '&' '*'
	'('
	':'
	' '
	' '
	'_'
	'+'
	'{'
	'}'
	'|'
	0
	'?'
	'>'
	'"'
	'<'
	'~'
endtable