#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

var AMouseDev 0
var AMouseCount 0

var AMouseCDev 0

var AMouseCallbacks 0

fn AMouseInit { -- }
	DeviceNew
		"mouse" DSetName

		DevCurrent@ AMouseDev!
	DeviceExit

	ListCreate AMouseCallbacks!
end

const AMouseMID 0x4D4F5553

fn AMousePoll { -- }
	auto i
	1 i!

	while (i@ 16 <)
		auto rs
		InterruptDisable rs!

		if (i@ AmanatsuPoll AMouseMID ==)
			auto hmm
			5 Calloc hmm!

			AMouseDev@ DeviceSelectNode
				DeviceNew
					AMouseCount@ hmm@ itoa

					hmm@ DSetName

					i@ "amanatsu,unit" DAddProperty

					if (AMouseCount@ 0 ==)
						DevCurrent@ AMouseCDev!
						(* pointerof AMouseInterruptASM i@ AmanatsuSetInterrupt "interrupt#" DAddProperty *)

						pointerof AMouseAddCallbackM "addCallback" DAddMethod
						pointerof AMouseEventM "getEvent" DAddMethod
						pointerof AMouseResetM "reset" DAddMethod
					end

					"mouse" "deviceType" DAddProperty

					"AISA,mouse" "model" DAddProperty

					1 "revision" DAddProperty

					i@ 4 + "interrupt#" DAddProperty

					1 AMouseCount +=
				DeviceExit
			DeviceExit
		end

		rs@ InterruptRestore

		1 i +=
	end

	if (AMouseCDev@ 0 ~=)
		AMouseReset
	end
end

fn private AMouseAddCallbackM { argvt argcn -- out1 out2 out3 }
	argvt@ @
	AMouseAddCallback
end

fn AMouseAddCallback { handler -- }
	handler@ AMouseCallbacks@ ListInsert
end

fn private AMouseEventM { argvt argcn -- out1 out2 out3 }
	AMouseEvent out2! out1!
end

fn AMouseEvent { -- detail event }
	auto id
	"amanatsu,unit" DGetProperty id!

	auto rs
	InterruptDisable rs!

	id@ AmanatsuSelectDev
	1 AmanatsuCommand
	AmanatsuReadB detail!
	AmanatsuReadA event!

	rs@ InterruptRestore
end

fn private AMouseResetM { argvt argcn -- out1 out2 out3 }
	AMouseReset
end

fn AMouseReset { -- }
	auto id
	AMouseCDev@ DeviceSelectNode
		"amanatsu,unit" DGetProperty id!
	DeviceExit

	auto rs
	InterruptDisable rs!

	id@ AmanatsuSelectDev
	2 AmanatsuCommand

	rs@ InterruptRestore
end

fnptr MouseCallback { detail event -- }

fn AMouseInterrupt { -- }
	auto id
	AMouseCDev@ DeviceSelectNode
		"amanatsu,unit" DGetProperty id!
	DeviceExit

	id@ AmanatsuSelectDev
	1 AmanatsuCommand

	auto event
	AmanatsuReadA event!

	auto detail
	AmanatsuReadB detail!

	auto n
	AMouseCallbacks@ ListHead n!

	while (n@ 0 ~=)
		detail@ event@ n@ ListNodeValue MouseCallback

		n@ ListNodeNext n!
	end
end