#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

procedure Main (* -- *)
	auto kbn
	"/keyboard" DevTreeWalk kbn!
	
	auto cn
	"/clock" DevTreeWalk cn!

	"verbose?" NVRAMGetVar dup if (0 ==)
		drop "true" "verbose?" NVRAMSetVar
		"true"
	end

	auto fv

	0 fv!

	"true" strcmp fv!

	if (fv@ ~~ kbn@ 0 ~= &&)
		kbn@ DeviceSelectNode
			5 "isDown" DCallMethod drop fv!
			80 "isDown" DCallMethod drop fv@ && fv!
		DeviceExit
	end

	if (fv@)
		ConsoleUserOut
	end

	"auto-boot?" NVRAMGetVar dup if (0 ==)
		drop "true" "auto-boot?" NVRAMSetVar
		"true"
	end

	if ("true" strcmp)
		auto cancel

		0 cancel!

		if (kbn@ 0 ~=)
			if (fv@)
				"\[[33mHold\[[0m \[[94mCTRL\[[0m \[[33mto halt automatic boot or \[[94mENTER\[[0m \[[33mto boot immediately...\[[0m\n" Printf

				kbn@ DeviceSelectNode
					50 80 5000 "waitKey" DCallMethod drop cancel!
				DeviceExit
			end
		end

		if (cancel@ ~~)
			auto r
			AutoBoot r!
			if (r@ 1 ~=)
				ConsoleUserOut
			end
			[r@]BootErrors@ "boot: %s\n" Printf
		end else
			ConsoleUserOut

			"user cancelled automatic boot\n" Printf
		end
	end else
		ConsoleUserOut

		"auto-boot?=false, not automatically booting\n" Printf
	end

	CR

	Monitor

	LateReset
end