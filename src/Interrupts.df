#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

table InterruptHandlers[64]

var SavedState 0
public SavedState

extern setvector { -- }

fn InterruptsInit { -- }
	setvector
end

asm "

setvector:
	la ev, exception_limn2k
	ret

exception_limn2k:
	sub sp, 112
	sgpr sp
	push epc
	push ers
	push lr

	mov t0, sp
	sub sp, 12
	mov long [sp + 8], t0
	mov long [sp + 4], 0
	mov long [sp], 0

	mov a3, ecause
	mov a2, epc
	mov a1, ers
	mov a0, badaddr
	jal Exception

	add sp, 12

	li badaddr, 0

	pop lr
	pop ers
	pop epc
	lgpr sp
	add sp, 112
	rfe

"

table ExceptionNames
	"Unknown"
	"Interrupt"
	"Syscall"
	"TLB refill"
	"Bus error"
	"Timer"
	"Breakpoint"
	"Invalid opcode"
	"Privilege violation"
	"Unaligned address"
	"Unaligned PC"
	"Division by zero"
	"Page fault"
endtable
const ExceptionCount 12

const Breakpoint 6

const ExtInterrupt 1

const LSICClaimComplete 0xF8030010

fnptr IntHandler { -- }

fn Exception { state ecause epc ers badaddr -- }
	if (ecause@ ExtInterrupt ==)
		auto int

		LSICClaimComplete@ int!

		while (int@)
			auto h
			[int@]InterruptHandlers@ h!

			if (h@)
				h@ IntHandler
			end else
				int@ "Spurious interrupt: %x\n" Problem
			end

			int@ LSICClaimComplete!

			LSICClaimComplete@ int!
		end

		return
	end

	ConsoleUserOut

	ecause@ "ECAUSE: %x\n" Printf
	epc@ "EPC: %x\n" Printf
	ers@ "ERS: %x\n" Printf
	badaddr@ "BADADDR: %x\n" Printf

	if (ConsoleInMethod@ 0 ~=)
		if (ecause@ Breakpoint ==)
			epc@ "\[[7mA3X: breakpoint at 0x%x, entering monitor\[[0m\n" Printf
			state@ DebugMonitor
			return
		end else
			state@ SavedState!
			[ecause@]ExceptionNames@ "%s.\n" Problem
		end
	end else
		epc@ [ecause@]ExceptionNames@ "\n!!!FAULT!!! %s, resetting.\n" Printf

		LateReset
	end
end

fn InterruptRegister { handler num -- }
	handler@ [num@]InterruptHandlers!
end

fn InterruptUnregister { num -- }
	0 [num@]InterruptHandlers!
end

asm "

InterruptEnable:
.global InterruptEnable
	bset rs, rs, 1
	ret

InterruptDisable:
.global InterruptDisable
	mov v0, rs
	bclr rs, rs, 3
	bclr rs, rs, 1
	ret

InterruptRestore:
.global InterruptRestore
	mov rs, a0
	ret

"