#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

table BootErrors
	0
	"ok"
	"not supported by device"
	"I/O error while searching for system software"
	"no system software found on disk"
	"I/O error while reading boot program"
	"invalid boot program"
	"no such device"
	"not enough memory"
	"system software error"
endtable
public BootErrors

procedure DevBootable (* devnode -- bootable? *)
	DevBootableR swap drop
end

procedure DevBootableR { devnode } (* -- brecord bootable? *)
	if (devnode@ 0 ==)
		7 return
	end

	auto brecord
	4096 Malloc brecord!

	devnode@ DeviceSelectNode
		auto ok
		brecord@ 1 "readBlock" DCallMethod
	DeviceExit

	ok!
	if (ok@ ~~)
		brecord@ Free
		2 return (* not supported *)
	end

	ok!
	if (ok@ ERR ==)
		brecord@ Free
		3 return (* failed to load boot record *)
	end

	if (brecord@ BootRecord_Magic + @ BootMagic == brecord@ BootRecord_ret + gb BootRM == && ~~)
		brecord@ Free
		4 return (* invalid boot record *)
	end

	brecord@ 1 return
end

procedure AutoBoot (* -- ok? *)
	auto bootnode

	"/bootdisk" DevTreeWalk bootnode!

	if (bootnode@ 0 ==)
		7 return
	end

	bootnode@ DeviceSelectNode
		"bootPath" DGetProperty "\[[33mautoboot:\[[0m \[[94m%s\[[0m\n" Printf
	DeviceExit

	bootnode@

	"boot-args" NVRAMGetVar dup if (0 ==)
		drop "" "boot-args" NVRAMSetVar
		""
	end

	CR

	BootNode
end

procedure BootNode { devnode args } (* -- ok? *)
	auto brecord
	auto ok

	devnode@ DevBootableR ok!

	if (ok@ 1 ~=)
		ok@ return
	end
	
	brecord!

	auto bblock
	auto bbc
	brecord@ BootRecord_BootBlockStart + @ bblock!
	brecord@ BootRecord_BootBlockCount + @ bbc!

	args@ brecord@ BootRecord_OSLabel + "\[[33mbooting: \[[0m\[[94m%s (%s)\[[0m\n" Printf

	brecord@ Free

	auto ptr
	BootBottom ptr!

	auto tmem
	"/memory" DeviceSelect
		"totalRAM" DGetProperty tmem!
	DeviceExit

	auto nmem
	ptr@ bbc@ 4096 * + nmem!

	if (nmem@ tmem@ >=)
		tmem@ nmem@ "at least %d bytes of contiguous memory are required to load this bootloader, only %d bytes present\n" Printf
		8 return
	end

	ptr@ bbc@ 4096 * "\[[33mloading boot program:\[[94m %d bytes at 0x%x\[[0m\n" Printf

	devnode@ DeviceSelectNode
		auto i
		0 i!

		while (i@ bbc@ <)
			ptr@ bblock@ "readBlock" DCallMethod drop ok!

			if (ok@ ERR ==)
				5 return (* failed to load boot blocks *)
			end

			1 bblock +=
			1 i +=
			ptr@ 4096 + ptr!
		end
	DeviceExit

	if (BootBottom@ BootMagic ~=)
		6 return
	end

	"\[[33mexecuting boot program!\[[0m\n" Printf

	API devnode@ args@ BootBottom 4 + @ asm "

	popv r5, r3

	popv r5, r2

	popv r5, r1

	popv r5, r0

	pusha

	li k0, 0

	call .layer
	b .out

	.layer:
	br r3

	.out:

	popa

	pushv r5, k0

	"

	auto retv
	retv!

	if (retv@ 0 ==)
		1 (* ok *) return
	end else
		9 (* system software error *) return
	end
end