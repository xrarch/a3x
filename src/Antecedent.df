#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

extern HeapInit { -- }
extern ConsoleEarlyInit { -- }
extern InterruptsInit { -- }
extern DeviceInit { -- }
extern ConsoleInit { -- }
extern FontInit { -- }
extern PlatformInit { -- }

extern Main { -- }

fn BIOSEntry { -- }
	HeapInit

	ConsoleEarlyInit
	(* print routines now work, do NOT use them before this *)

	LinkInfo

	if (NVRAMCheck ~~)
		"nvram corrupted! formatting\n" Printf
		NVRAMFormat
	end

	PlatformInit

	PlatformName@ "\nWelcome to %s\n" Printf

	DeviceInit

	InterruptsInit

	FirmwareBanner

	ConsoleInit

	FontInit

	Main
end

asm "

.entry BIOSEntry

"

asm "

;set exception vector to zero and immediately cause an exception
;this forces the CPU to reset and do whatever it needs to do that cleanly

Reset:
.global Reset
	li ev, 0
	div t0, t0, zero
.wait:
	b .wait

"

fn LinkInfo { -- }
	pointerof _text "_text = %x " Printf
	pointerof _text_size "_text_size = %x " Printf
	pointerof _text_end "_text_end = %x\n" Printf

	pointerof _data "_data = %x " Printf
	pointerof _data_size "_data_size = %x " Printf
	pointerof _data_end "_data_end = %x\n" Printf

	pointerof _bss "_bss = %x " Printf
	pointerof _bss_size "_bss_size = %x " Printf
	pointerof _bss_end "_bss_end = %x\n" Printf
end

fn LateReset { -- }
	"\[c" Printf
	Reset
end

fn FirmwareBanner { -- }
	'\n' Putc

	"/" DeviceSelect
		"author" DGetProperty "version" DGetProperty DGetName "%s %s by %s\n" Printf
		"buildDate" DGetProperty "build" DGetProperty "Build %s, built on %s\n" Printf
	DeviceExit

	"/platform" DeviceSelect
		"model" DGetProperty "Platform: %s\n" Printf
	DeviceExit

	"/cpu" DeviceSelect
		"model" DGetProperty "CPU: %s\n" Printf
	DeviceExit

	"/memory" DeviceSelect
		"totalRAM" DGetProperty 1024 / "RAM: %dkb\n" Printf
	DeviceExit

	'\n' Putc
end