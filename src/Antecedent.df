#include "<df>/dragonfruit.h"
#include "<inc>/a3x.h"

extern HeapInit
extern ConsoleEarlyInit
extern InterruptsInit
extern DeviceInit
extern ConsoleInit
extern FaultsRegister
extern FontInit
extern AUCInit
extern PlatformInit

extern Main

(* MUST be at the top *)
procedure AntecedentEntry (* -- *)
	HeapInit

	ConsoleEarlyInit
	(* print routines now work, do NOT use them before this *)

	LinkInfo

	"Welcome to ANTECEDENT 3.x\n" Printf

	if (NVRAMCheck ~~)
		"nvram corrupted! formatting\n" Printf
		NVRAMFormat
	end

	PlatformInit

	InterruptsInit

	AUCInit

	DeviceInit

	FirmwareBanner

	ConsoleInit

	FaultsRegister (* let llfw handle faults up until here *)

	FontInit

	Main
end

asm "

Reset:
.global Reset
	li ivt, 0
	divi r0, r0, 0
.loop:
	b .loop

"

procedure LinkInfo (* -- *)
	pointerof _text "_text = %x " Printf
	pointerof _text_size "_text_size = %x " Printf
	pointerof _text_end "_text_end = %x\n" Printf

	pointerof _data "_data = %x " Printf
	pointerof _data_size "_data_size = %x " Printf
	pointerof _data_end "data_end = %x\n" Printf

	pointerof _bss "_bss = %x " Printf
	pointerof _bss_size "_bss_size = %x " Printf
	pointerof _bss_end "_bss_end = %x\n" Printf
end

procedure LateReset (* -- *)
	"\[c" Printf
	Reset
end

procedure BannerPrint (* ... -- *)
	Printf
end

procedure FirmwareBanner (* -- *)
	'\n' Putc

	"/" DeviceSelect
		"author" DGetProperty "version" DGetProperty DGetName "Implementation details: %s %s written by %s\n" BannerPrint
		"buildDate" DGetProperty "build" DGetProperty "Build %s, built on %s\n" BannerPrint
	DeviceExit

	"/platform" DeviceSelect
		"model" DGetProperty "Platform: %s\n" BannerPrint
	DeviceExit

	"/cpu" DeviceSelect
		"type" DGetProperty "CPU: %s\n" BannerPrint
	DeviceExit

	"/memory" DeviceSelect
		"totalRAM" DGetProperty 1024 / "RAM: %dkb\n" BannerPrint
	DeviceExit

	'\n' Putc
end